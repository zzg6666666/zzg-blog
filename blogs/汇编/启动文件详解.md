---
title: 启动文件详解
date: 2024/7/14
tags:
 - cortex-m3
categories:
 - stm32
---


主要分析的文件是STM32CubeMx生成，芯片是STM32F103C8T6。

启用文件的作用是初始化内存，将已经初始化了的变量的值，从flash复制到ram中，为初始化的变量的内存设置为0，初始化c++相关的全局变量，定义向量表和中断函数。

### 简略文件

删除了注释的简略的的启动文件如下：

```assembly
  .syntax unified
  .cpu cortex-m3
  .fpu softvfp
  .thumb

.global g_pfnVectors
.global Default_Handler

.word _sidata

.word _sdata

.word _edata

.word _sbss

.word _ebss

.equ  BootRAM, 0xF108F85F

  .section .text.Reset_Handler
  .weak Reset_Handler
  .type Reset_Handler, %function
Reset_Handler:

  bl  SystemInit


  ldr r0, =_sdata
  ldr r1, =_edata
  ldr r2, =_sidata
  movs r3, #0
  b LoopCopyDataInit

CopyDataInit:
  ldr r4, [r2, r3]
  str r4, [r0, r3]
  adds r3, r3, #4

LoopCopyDataInit:
  adds r4, r0, r3
  cmp r4, r1
  bcc CopyDataInit
  
  ldr r2, =_sbss
  ldr r4, =_ebss
  movs r3, #0
  b LoopFillZerobss

FillZerobss:
  str  r3, [r2]
  adds r2, r2, #4

LoopFillZerobss:
  cmp r2, r4
  bcc FillZerobss

    bl __libc_init_array
    
  bl main
  bx lr
.size Reset_Handler, .-Reset_Handler

    .section .text.Default_Handler,"ax",%progbits
Default_Handler:
Infinite_Loop:
  b Infinite_Loop
  .size Default_Handler, .-Default_Handler
  
  .section .isr_vector,"a",%progbits
  .type g_pfnVectors, %object
  .size g_pfnVectors, .-g_pfnVectors


g_pfnVectors:

  .word _estack
  .word Reset_Handler
  .
  .
  .
  .word BootRAM

  .weak NMI_Handler
  .thumb_set NMI_Handler,Default_Handler
  .
  .
  .
  .weak USBWakeUp_IRQHandler
  .thumb_set USBWakeUp_IRQHandler,Default_Handler

```

### 文件头

首先，启动文件做了一些描述，告诉编译器该文件的语法等信息

```assembly
  /*使用arm汇编和thumb汇编*/
  .syntax unified
  /* CPU型号 */
  .cpu cortex-m3
  /* 软件FPU */
  .fpu softvfp
  /* 接下来的指令使用 thumb汇编 */
  .thumb
```

  **.syntax unified**:启用统一语法格式，允许在一个汇编中使用arm和thumb汇编。在某些情况下，可能需要设置为`  .syntax divided`,启用传统的语法模式。

 **.cpu cortex-m3**：指定目标处理器为 Cortex-M3，通过**.cpu**指令可以让编译器在编译的时候进行优化。你也能会看到这样的汇编指令：**.arch armv7-m**，这指定了处理的架构。**.CPU**和**.arch**汇编指令，都是让编译器为特定的架构进行优化，在通常情况下，只使用一个汇编命令就足够了。

**.fpu softvfp**：指定浮点的处理方式，也可以在makefile中指定，M3架构是没有硬件fpu，因此使用软件fpu。

**.thumb**:指定接下来的汇编语法为thumb汇编

```assembly
.global g_pfnVectors
.global Default_Handler
```

声明全局符号，`g_pfnVectors`和`Default_Handler`,对链接文件暴露。g_pfnVectors是全局向量表，Default_Handler是默认的异常处理函数。

### 使用的变量

```assembly
/* start address for the initialization values of the .data section. 
defined in linker script */
.word  _sidata
/* start address for the .data section. defined in linker script */  
.word  _sdata
/* end address for the .data section. defined in linker script */
.word  _edata
/* start address for the .bss section. defined in linker script */
.word  _sbss
/* end address for the .bss section. defined in linker script */
.word  _ebss
/* stack used for SystemInit_ExtMemCtl; always internal RAM used */

.equ  BootRAM, 0xF108F85F
```

**.word**：一个32位的地址，这里的5个符号都是在链接文件定义，会在链接文件中解释。

**.equ**：相当于C语言的**#define**

### Reset_Handler的定义

```assembly
  .section .text.Reset_Handler
  .weak Reset_Handler
  .type Reset_Handler, %function
Reset_Handler:

/* Call the clock system initialization function.*/
   bl  SystemInit
```

**.section**定义一个段，将会占用一些RAM/ROM空间，**.text**表示该段是代码段。` .section .text.Reset_Handler`表示定义一个叫做**Reset_Handler**函数的代码段。

**.weak**表示该函数是弱定义，如果在其他地方定义过该函数，那么不使用这里的函数。

**.type**：告诉编译器该段的类型，**%function**表示该段是函数类型，让编译器进行优化。

**Reset_Handler:** Reset_Handler函数的代码从这里开始。

```assembly
/* Call the clock system initialization function.*/
  bl  SystemInit

/* Copy the data segment initializers from flash to SRAM */
  ldr r0, =_sdata   //数据段在内存中的开始地址
  ldr r1, =_edata	  //数据段在内存中的结束地址
  ldr r2, =_sidata 	//数据段在flash的储存地址
  movs r3, #0
  b LoopCopyDataInit
```

` bl  SystemInit`：执行SystemInit函数，该函数的作用不在讨论内容中。

`ldr r0, =`_sdata ...：加载数据段先关的地址到寄存器，同时设置r3寄存器的值为0。

`b LoopCopyDataInit`:执行LoopCopyDataInit。

#### 初始化.data

```assembly
CopyDataInit:
	//将r2寄存器加上r3寄存器的值，在ROM中对应的值加载到r4寄存器中
  ldr r4, [r2, r3]
  //将r4寄存器的值存到，r0寄存器加r3寄存器，在ram的位置
  str r4, [r0, r3]
  adds r3, r3, #4

LoopCopyDataInit:
	//r4等于_sdata地址加 r3 
  adds r4, r0, r3
  //比较r4寄存器和_edata的地址，并设置C标志位
  cmp r4, r1
  //C标志位为0就执行
  bcc CopyDataInit
  
/* Zero fill the bss segment. */
```

这段代码的作用是初始化.data段的值。

`LoopCopyDataInit`函数相当于一个for循环，在r4寄存器的值小于_edata时，执行`CopyDataInit`函数。大于等于就执行`/* Zero fill the bss segment. */`后面的代码。

`CopyDataInit`函数的作用是将 flash 的值加载到 ram 中。

#### 初始化.bss

```assembly
/* Zero fill the bss segment. */
	//加载bss段的开始地址到r2
  ldr r2, =_sbss
  //加载bss段的开始地址到r4
  ldr r4, =_ebss
  //设置r3寄存器的值
  movs r3, #0
  b LoopFillZerobss

FillZerobss:
	//将r3寄存器(0)，存到r2寄存器对应的ram中
  str  r3, [r2]
  //r2 累加 4
  adds r2, r2, #4

LoopFillZerobss:
	//比较r3 和 r4寄存器的值
  cmp r2, r4
  //r2 小于 r4就执行
  bcc FillZerobss
```

这段代码的作用是初始化.bss段的值。代码逻辑和前面一样。不一样的是bss段的值都设置为0。

#### C++相关的初始化与调用main

```assembly
/* Call static constructors */
    bl __libc_init_array
/* Call the application's entry point.*/
  bl main
  bx lr
.size Reset_Handler, .-Reset_Handler
```

`bl __libc_init_array`:初始化C++相关的全局和静态对象。由编译器提供。

`bl main`:将会执行main函数，同时设置lr寄存器。

`bx lr`当执行完成main函数后，会执行这行

`.size Reset_Handler, .-Reset_Handler`，`.size`计算Reset_Handler段的大小，告诉给链接文件该.text类型的段大小。

### Default_Handler

```assembly
    .section .text.Default_Handler,"ax",%progbits
Default_Handler:
Infinite_Loop:
  b Infinite_Loop
  .size Default_Handler, .-Default_Handler
```

定义一个代码段，`Default_Handler`，这是默认的异常处理函数，为死循环。`a`表示该段可以被分配到程序中，`x`表示该段包含可执行代码，`%progbits`是`.type`,表示该段包含有用的数据或代码。

### 异常向量表

```assembly
  .section .isr_vector,"a",%progbits
  .type g_pfnVectors, %object
  .size g_pfnVectors, .-g_pfnVectors
```

`.section .isr_vector,"a",%progbits`，定义`.isr_vector`段，`a`表示该段是可分配的%progbits`表示该段含有数据或者代码。

` .type g_pfnVectors, %object`，在`.isr_vector`段存储了`g_pfnVectors`,`%object`表示`g_pfnVectors`为数据对象（变量或数组。

`  .size g_pfnVectors, .-g_pfnVectors`计算`g_pfnVectors`的大小，计算方式是当前位置减去`g_pfnVectors`的定义位置。

```assembly
g_pfnVectors:

  .word _estack
  .word Reset_Handler
  .
  .
  .
  .word BootRAM

```

定义`g_pfnVectors`向量表，每个元素占用了32位的大小，向量表的第一个元素是栈顶的位置，除了最后一个元素，剩下的都是各种异常中断的函数地址。

M3、M4内核在上电后，会将向量表的第一个元素加载到SP寄存器中，然后执行向量表的第二个元素。

### 异常Handler

```assembly
  .weak NMI_Handler
  .thumb_set NMI_Handler,Default_Handler
  .
  .
  .
  .weak USBWakeUp_IRQHandler
  .thumb_set USBWakeUp_IRQHandler,Default_Handler
```

`.weak NMI_Handler`表示NMI_Handler是一个弱定义函数，如果在其他地方定义，则不使用此处定义的函数。

`.thumb_set NMI_Handler,Default_Handler`设置`NMI_Handler`的地址为`Default_Handler`的地址。

